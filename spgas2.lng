MODEL:

! See: http://stoprog.org/index.html?spintroduction.html

In the first year the gas company buys gas from the market, 
delivers some to its customers right away and puts the rest 
in storage for next year. The following year the company can 
supply from storage or buy from the market. The decision 
variables are: 

1) how much gas to purchase and deliver, 
2) how much gas to purchase and store, and 
3) how much gas to take from storage and deliver to customers. 

The decision depends on the price of gas in year 1 and year 2, 
the storage cost (say $1 per unit per year), the size of the 
storage facility, and the demand in each period. With this 
information the problem can be modelled as a simple linear 
program with the objective to minimize overall cost. In practice 
the price and demand in year 2 will be uncertain. Suppose that 
year 1 is a normal year and that year 2 can be one of three 
equally likely outcomes: normal, cold, or very cold. Each of 
these outcomes has different data as shown in the following table: 
  
Outcome     Probability      Gas Cost ($)       Demand (units) 
Normal          1/3              5.0                 100 
Cold            1/3              6.0                 150 
Very Cold       1/3              7.5                 180 

Forming and solving the stochastic linear program gives the following solution: 
  Year  Outcome    Purchase to Use  Purchase to Store  Storage  Cost ($)  
    1    Normal          100              100          100     1100  
    2    Normal            0                0            0        0  
    2    Cold             50                0            0      300  
    2    Very Cold        80                0            0      600  

Expected Cost/OV = $1400 
;

! Core Model -------------------------------------------------------+;

SETS:
   TIME: INVENTORY, COST, DEMAND,  PURCHASE;
ENDSETS

DATA:
   HOLDC = 1;
   TIME = 1..2;
   DEMAND = 100,;
   COST = 5,;
ENDDATA

MIN = PURCHASE_COST + HOLD_COST;

PURCHASE_COST = @SUM( TIME( T): COST( T) * PURCHASE( T));
HOLD_COST = @SUM( TIME( T): HOLDC * INVENTORY( T));

INVENTORY( 1) = PURCHASE( 1) - DEMAND( 1);
@FOR( TIME( T) | T #GT# 1: 
   INVENTORY( T) = INVENTORY( T - 1) + PURCHASE( T) - DEMAND( T);
);

! SP Declarations --------------------------------------------------+;

! Construct the outcome table;
SETS:
   OUTCOMES: O_COST, O_DEMAND;
ENDSETS
DATA:
   OUTCOMES, O_COST, O_DEMAND =
     NORMAL     5.0     100
     COLD       6.0     150
     VCOLD      7.5     180;
ENDDATA
 
! The purchasing decisions are the recourse/stoch variables ;
@FOR( TIME( T):
   @SPSTGVAR( T - 1, PURCHASE( T));
);

! Random variables are the gas demand and cost for T > 1;
@FOR( TIME( T) | T #GT# 1:
   @SPSTGRNDV( T - 1, DEMAND( T));
   @SPSTGRNDV( T - 1, COST( T));
);

! Define the (cost,demand) joint outcome table;
@SPTABLESHAPE( 'DST_CST_DMD', @SIZE( OUTCOMES), 2);

! Load the outcomes into the table;
@FOR( OUTCOMES( I):
   @SPTABLEOUTC( 'DST_CST_DMD', 1/@SIZE( OUTCOMES), O_COST( I), O_DEMAND( I))
);

@FOR( TIME( T) | T #GT# 1:

! Declare a specific instance of our outcome table;
   @SPTABLEINST( 'DST_CST_DMD', 'DST_CST_DMD_' + TIME( T));

! Bind random variables, COST and DEMAND, to the instance;
   @SPTABLERNDV( 'DST_CST_DMD_' + TIME( T), COST( T), DEMAND( T));
);

END
