MODEL:
! Portfolio via Scenarios Model with Conditional Value at Risk Objective 
  Formulated as a Stochastic Program.
Definition of CVaR:
  Given a parameter alpha, 0 < alpha < 1, and
variables: t = a threshold, and R(s) = return from scenario s,
if we Max = alpha*t - E{ max[0,t-R(s)]}, then
at an optimum t will be the largest value such that,
Prob{ R(s) <= t} <= alpha.
We say t is the VaR for the given probability alpha;
SETS:
  INST: X, RETURN, SERET;
  SCENE: R;
  SXI( SCENE, INST): SIRET;
ENDSETS
DATA:
 BUDGET = 1;
 TARGRET = 1.15;
 ALPHA = .05;   ! Prob{ Portfolio ending value <= THRESHOLD};
 INST = ATT GMC USX;
 SCENE = 1..20;
 SIRET =
1.3	1.225	1.149	
1.103	1.29	1.26	
1.216	1.216	1.419	
0.954	0.728	0.922	
0.929	1.144	1.169	
1.056	1.107	0.965	
1.038	1.321	1.133	
1.089	1.305	1.732	
1.28	1.225	1.149	
1.103	1.28	1.26	
1.216	1.216	1.4	
0.954	0.738	0.912	
0.929	1.1	1.169	
1.056	1.107	0.965	
1.038	1.3	1.133	
1.1	1.305	1.732	
1.09	1.195	1.03	
1.073	1.39	1.131	
1.035	0.928	1.006	
1.176	1.715	1.908	;
ENDDATA

   @FOR( INST(j): [R_SERET]
    SERET(j) = @SUM( SCENE(s): SIRET(s,j))/@SIZE(SCENE);
      );
! X(j) and t are stage 0 decision variables,
  SIRET(s,j) is a stage 1 random variable,
  Shortfall(s) is a stage 1 decision variable;
  [R_BUDGET]  @SUM( INST(j): X(j)) = BUDGET;
  [R_TARGRET] @SUM( INST(j): SERET(j)*X(j)) = TARGRET;

  @FOR(SCENE(s):
    [R_TTL_RETURN] TTL_RETURN = @SUM( INST(j): RETURN(j)*X(j));
    [R_SHORTFALL] SHORTFALL >= THRESHOLD - TTL_RETURN;
       );
  [R_OBJ] MAX = ALPHA*THRESHOLD - SHORTFALL;

! The recourse/stochastic variables;
@SPSTGVAR( 0, THRESHOLD);
@FOR( INST( I): 
   @SPSTGVAR( 0, X( I));
);

@FOR( INST( I): 
   @SPSTGRNDV( 1, RETURN( I));
);

! Declare the distributions and their random variables;
@SPDISTTABLE( SIRET, RETURN);

END
