MODEL:
SETS:
   PLANES/ ROCKET, METEOR, STREAK, 
    COMET, JET, BIPLANE /: 
     PROFIT, SETUP, QUANTITY, BUILD;
   RESOURCES /STEEL, COPPER, PLASTIC,
    RUBBER, GLASS, PAINT/: AVAILABLE;
   RXP( RESOURCES, PLANES): USAGE;
ENDSETS
DATA:
   PROFIT SETUP =
     30     35
     45     20
     24     60
     26     70
     24     75
     30     30;     
   AVAILABLE = 
    800 1160 1780 1050 1360 1240;
   USAGE =  1 4 0 4 2 0
            4 5 3 0 1 0
            0 3 8 0 1 0
            2 0 1 2 1 5
            2 4 2 2 2 4
            1 4 1 4 3 4;
ENDDATA

MAX = @SUM( PLANES: 
 PROFIT * QUANTITY - SETUP * BUILD);

@FOR( RESOURCES( I):
 @SUM( PLANES( J): 
  USAGE( I, J) * QUANTITY( J)) <= 
   AVAILABLE( I)
);
@FOR( PLANES:
 QUANTITY <= 400 * BUILD;
  @BIN( BUILD)
);
@FOR( PLANES:
 @GIN( QUANTITY)
);

END
