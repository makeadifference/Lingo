MODEL:
! A 6 Warehouse 8 Vendor Transportation Problem;
SETS:
   WAREHOUSES: CAPACITY;
   VENDORS: DEMAND;
   LINKS( WAREHOUSES, VENDORS): COST, VOLUME;
ENDSETS
! Here is the data;
DATA:
   !set members;
   WAREHOUSES = WH1 WH2 WH3 WH4 WH5 WH6;
   VENDORS = V1 V2 V3 V4 V5 V6 V7 V8;

   !attribute values;
   CAPACITY = 60 55 51 43 41 52;
   DEMAND = 35 37 22 32 41 32 43 38;
   COST = 6 2 6 7 4 2 5 9
          4 9 5 3 8 5 8 2
          5 2 1 9 7 4 3 3
          7 6 7 3 9 2 7 1
          2 3 9 5 7 2 6 5
          5 5 2 2 8 1 4 3;
ENDDATA
! The objective;
   MIN = @SUM( LINKS( I, J): 
    COST( I, J) * VOLUME( I, J));
! The demand constraints;
   @FOR( VENDORS( J): 
    @SUM( WAREHOUSES( I): VOLUME( I, J)) = 
     DEMAND( J));
! The capacity constraints;
   @FOR( WAREHOUSES( I): 
    @SUM( VENDORS( J): VOLUME( I, J)) <= 
     CAPACITY( I));
END
