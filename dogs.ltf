! Have LINGO echo input to the screen
SET ECHOIN 1
! Suppresses the standard solution report
SET TERSEO 1
! Begins input of a new model
MODEL:
SETS:
   DAYS / MON TUE WED THU FRI SAT SUN/:
    REQUIRED, START;
ENDSETS

DATA:
   REQUIRED = @FILE( 'PLUTO.LDT');
   @TEXT( 'PLUTO.TXT') = 
    @WRITEFOR( DAYS: START);
ENDDATA

MIN = @SUM( DAYS( I): START( I));

@FOR( DAYS( J): 
 @SUM( DAYS( I) | I #LE# 5: 
  START( @WRAP( J - I + 1, 7)))
   >= REQUIRED( J)
);

@FOR( DAYS: @GIN( START));
END
! Solve Pluto Dogs model
GO
! Alter model for Mars
ALTER ALL 'PLUTO'MARS'
! Solve Mars model
GO
! Alter model for Saturn
ALTER ALL 'MARS'SATURN'
! Solve Saturn model
GO
! Restore parameters
SET TERSEO 0
SET ECHOIN 0
