MODEL:
! General Equilibrium Model of an economy;
! Data based on Kehoe, Math Prog, Study 23(1985);
! Find clearing prices for commodities/goods and
  equilibrium production levels for processes in
  an economy;
SETS:
 GOOD/1..4/: PRICE, H;
 SECTOR/1..4/;
 GXS( GOOD, SECTOR): ALPHA, W;
 PROCESS/1..2/: LEVEL;
 GXP( GOOD, PROCESS): MAKE;
ENDSETS
DATA:
! Demand curve parameter for each good and SECTOR;
 ALPHA =
   .5200  .8600  .5000  .0600
   .4000  .1     .2     .25
   .04    .02    .2975  .0025
   .04    .02    .0025  .6875;
! Initial wealth of Good I by Market J;
  W =
   50     0      0      0
    0    50      0      0
    0     0    400      0
    0     0      0    400;
! Amount produced of good I by process J;
  MAKE =
      6   -1
     -1    3
     -4   -1
     -1   -1;
! Weights for price normalization constraint;
  H = .25 .25 .25 .25;
ENDDATA
CALC:
! Set the feasibility tolerance;
  @SET( 'FNFTOL', .001);
ENDCALC
!----------------------------------------------------------;
! Model based on Stone, Tech. Rep. Stanford OR(1988);
! Minimize the artificial variable;
MIN = V;
! Supply is >= demand;
@FOR( GOOD( G):
 @SUM( SECTOR( M): W( G, M))
  + @SUM( PROCESS( P): MAKE( G, P) * LEVEL( P))
   - @SUM( SECTOR( S):
    ALPHA( G, S) * @SUM( GOOD( I): PRICE( I) *
     W( I, S))/ PRICE( G)) + H( G) * V >= 0;
);
! Each process at best breaks even;
@FOR( PROCESS( P):
 @SUM( GOOD( G): - MAKE( G, P) * PRICE( G)) >= 0;
);
! Prices scale to 1;
@SUM( GOOD( G): - H( G) * PRICE( G)) = -1;
END
