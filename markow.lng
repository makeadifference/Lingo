MODEL:

SETS:
   STOCKS: AVG_RET, WEIGHT;
   DAYS;
   SXD( DAYS, STOCKS): RETURN;
   SXS( STOCKS, STOCKS): COVR, CORR;
ENDSETS

DATA:
   DAYS   = 1..12;
   TARGET = .15;
   STOCKS =   ATT     GMC      USX;
   RETURN = 0.300   0.225    0.149
            0.103   0.290    0.260
            0.216   0.216    0.419
           -0.046  -0.272   -0.078
           -0.071   0.144    0.169
            0.056   0.107   -0.035
            0.038   0.321    0.133
            0.089   0.305    0.732
            0.090   0.195    0.021
            0.083   0.390    0.131
            0.035  -0.072    0.006
            0.176   0.715    0.908;
ENDDATA

CALC:
   !Average annual return for each stock;
   @FOR( STOCKS( S): 
    AVG_RET( S) = 
     ( @SUM( SXD( D, S): RETURN( D, S)) / 
      @SIZE( DAYS))
   );

   !Covariance matrix;
   @FOR( SXS( S1, S2): 
    COVR( S1, S2) = 
     @SUM( DAYS( D):( RETURN( D, S1) - AVG_RET( S1)) * 
      ( RETURN( D, S2) - AVG_RET( S2))) / @SIZE( DAYS)
   );

   !Although not required, compute the correlation matrix;
   @FOR( SXS( S1, S2): 
    CORR( S1, S2) = COVR( S1, S2) / 
    ( COVR( S1, S1) * COVR( S2, S2))^.5;
   );
ENDCALC

!Minimize the risk of the portfolio 
 (i.e., its variance);
[R_OBJ] MIN = @SUM( SXS( S1, S2): 
 WEIGHT( S1) * WEIGHT( S2) * COVR( S1, S2));

!Must be fully invested;
[R_BUDGET] @SUM( STOCKS: WEIGHT) = 1;

!Must exceed target return;
[R_TARGET] @SUM( STOCKS: AVG_RET * WEIGHT) >= TARGET;

END
